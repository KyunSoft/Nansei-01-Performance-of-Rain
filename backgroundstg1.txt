task TBackgroundStg {
	let EndStgID = "EndStgScript"~ToString(GetOwnScriptID);

	let dir = GetCurrentScriptDirectory;
	let bg_path = dir ~ "img/stg1/bg11.png";	
	let bgfog_path = dir ~ "img/stg1/bg13.png";
	let fog_path = dir ~ "img/stg1/bgfog10.png";
	let dark_path = dir ~ "img/stg1/bg14.png";
	let tree_path = dir ~ "img/stg1/bg12.png";
	let tree2_path = dir ~ "img/stg1/bg12_2.png";
	let tree3_path = dir ~ "img/stg1/bg12_3.png";

	let objfog = ObjPrim_Create(OBJ_SPRITE_2D);
	let objdark = ObjPrim_Create(OBJ_SPRITE_2D);

	let c = 0;
	let c2 = 0;
	let camA = 0;

	let darkalpha = 250;
	let A = 0;
	let taskON = true;
	let spellactive = false;
	
	CreateFogpicture;
	
	task CreateFogpicture{
		let moveX=0;
		
		ObjPrim_SetTexture(objfog, fog_path); 
		Obj_SetRenderPriority(objfog, 0.22);
		ObjSprite2D_SetSourceRect(objfog,0+moveX,0,384+moveX,448);
		ObjSprite2D_SetDestRect(objfog,0,0+moveX,384,480+moveX);
		ObjRender_SetAlpha(objfog,200);
		
		loop{
			ObjSprite2D_SetSourceRect(objfog,0+moveX,0,384+moveX,448);
			//ObjSprite2D_SetDestRect(objfog,0+moveX,0,384+moveX,480);
			moveX+=1;
			yield;
		}
	}

	ObjPrim_SetTexture(objdark, dark_path); 
	Obj_SetRenderPriority(objdark, 0.24);
	ObjSprite2D_SetSourceRect(objdark,0,0,384,448);
	ObjSprite2D_SetDestRect(objdark,0,0,384,480);
	ObjRender_SetAlpha(objdark,250);

	stgbg1(255, 8.5, 1);
	stgb3;
	while(GetCommonData(EndStgID,false)==false) {
		spellactive = GetCommonData("spellactive",false);
		if(spellactive == false) {
			ObjRender_SetAlpha(objfog,255);
			SetCameraPitch(2*cos(camA));
			camA++;
		} else {
			ObjRender_SetAlpha(objfog,0);
		}
		if(GetCommonData("Stg1BgStep",0)==0){
			c++;
			if(c == 1) {
				stgbg3(tree3_path, 2.5, 0, -240, 0.5, 90, 0.20);
				stgbg3(tree3_path, 2.5, 0, 80, 0.5,- 90, 0.20);
			}
			if(c == 16) {
				stgbg3(tree_path, 3, 0, -280, 0.4, -90, 0.21);
				stgbg3(tree_path, 3, 0, 120, 0.4, 90, 0.21);
			}
			if(c == 31) {
				stgbg3(tree2_path, 3.5, 0, -280, 0.3, -90, 0.21);
				stgbg3(tree2_path, 3.5, 0, 120, 0.3, 90, 0.21);
				c = -15;
			}
		}
		if(c2 < 600) {c2++;}
		if(c2>100 && c2 < 600) {
			darkalpha -= 250/500;
			darkalpha = max(darkalpha, 0);
		}
		ObjRender_SetAlpha(objdark,darkalpha);
		yield;
	}

	Obj_Delete(objfog);
// STAGE GROUND
	task stgbg1(alpha,yadd, Z){
		let objbg = ObjPrim_Create(OBJ_SPRITE_3D);
		let a2 = 75;
		let ymove = 0;
		ObjRender_SetBlendType(objbg,BLEND_ALPHA);
		Obj_SetRenderPriority(objbg,0.20);
		ObjPrim_SetTexture(objbg,bg_path);
		SetCameraFocusXYZ(0,0,0);
		ObjSprite3D_SetSourceRect(objbg, 0,0,256,16*256);
		ObjSprite3D_SetSourceDestRect(objbg, -612, 10*-512, 612, 6*512);
		ObjRender_SetScaleXYZ(objbg,1,1,1);
		ObjRender_SetPosition(objbg, 0,-480,0);
		while(GetCommonData(EndStgID,false)==false || !Obj_Delete(objbg)){
			yield;
			if(spellactive == false) {
				ObjRender_SetAlpha(objbg,alpha);
				ObjRender_SetAngleXYZ(objbg, 80, a2, 0);
				ObjSprite3D_SetSourceRect(objbg, 0, 0+ymove*0.75, 256, 16*256+ymove*0.75);
				ymove -= yadd;
			} else {
				ObjRender_SetAlpha(objbg,0);
			}
			if(GetCommonData("Stg1BgStep",0)==2){Obj_Delete(objbg);}
		}
		Obj_Delete(objbg);
	}
// STAGE FOG GROUND
	task stgb3{
		let objbg = ObjPrim_Create(OBJ_SPRITE_3D);
		let a2 = 75;
		let ymove = 0;
		ObjRender_SetBlendType(objbg,BLEND_ALPHA);
		Obj_SetRenderPriority(objbg,0.21);
		ObjPrim_SetTexture(objbg,bgfog_path);
		ObjSprite3D_SetSourceRect(objbg,0,0,256,256);
		ObjSprite3D_SetSourceDestRect(objbg,0,0,256*10,256*10);
		ObjRender_SetScaleXYZ(objbg,0.5,0.5,0.5);
		ObjRender_SetPosition(objbg,0,40,0);
		while(GetCommonData(EndStgID,false)==false || !Obj_Delete(objbg)){
			yield;
			if(spellactive == false) {
				ObjRender_SetAlpha(objbg,0);
				ObjRender_SetAngleXYZ(objbg, 70, a2, 0);
				ObjSprite3D_SetSourceRect(objbg, 0, 0+ymove*0.75, 256*10, 256*10+ymove*0.75);
				ymove -= GetCommonData("STGBG_speed", 1)*3;
			} else {
				ObjRender_SetAlpha(objbg,0);
			}
			if(GetCommonData("Stg1BgStep",0)==2){Obj_Delete(objbg);}
		}
		Obj_Delete(objbg);
	}
//STAGE LEAVES
	task stgbg3(path,yadd,Y,Z,scl,aX,prio) {
		let objbg = ObjPrim_Create(OBJ_SPRITE_3D);
		let a2 = 90;
		let ymove = 0;
		let alpha = 255;
		ObjRender_SetBlendType(objbg,BLEND_ALPHA);
		Obj_SetRenderPriority(objbg,prio);
		ObjPrim_SetTexture(objbg,path);
		ObjSprite3D_SetSourceRect(objbg,1,1,2*256,2*256);
		ObjSprite3D_SetSourceDestRect(objbg,1,1,2*256,2*256);
		ObjRender_SetScaleXYZ(objbg,scl, scl, scl);
		ObjRender_SetAngleXYZ(objbg, aX, 20, aX);
		ObjRender_SetPosition(objbg,-470-ymove, 80+Y, -60+Z-0.3*ymove);
		ObjRender_SetAlpha(objbg,0);
		while(!Obj_IsDeleted(objbg)){
			if(spellactive == false) {
				ObjRender_SetAlpha(objbg,alpha);
				ObjRender_SetPosition(objbg, -470-ymove, 80+Y, -60+Z-0.3*ymove);
			} else {
				ObjRender_SetAlpha(objbg,0);
			}
			ymove -= yadd+0.03*Y;
			if(ymove<-1000) {Obj_Delete(objbg);}
			if(GetCommonData(EndStgID,false)==true){Obj_Delete(objbg);}
			if(GetCommonData("Stg1BgStep",0)==2){Obj_Delete(objbg);}
			yield;
		}
	}
}

